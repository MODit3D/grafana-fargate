data "aws_ami" "ubuntu" {
  most_recent = true

  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-xenial-16.04-amd64-server-*"]
  }

  filter {
    name   = "virtualization-type"
    values = ["hvm"]
  }

  owners = ["099720109477"] # Canonical
}

resource "aws_instance" "bastion" {
  count                       = var.bastion_count
  ami                         = data.aws_ami.ubuntu.id
  instance_type               = "t2.micro"
  key_name                    = var.key
  vpc_security_group_ids      = [aws_security_group.bastion.id]
  associate_public_ip_address = true
  subnet_id                   = var.bastion_subnet
  user_data                   = data.template_file.cloud_config.rendered

  tags = {
    Name = "bastion_host"
  }
}

data "template_file" "cloud_config" {
  template = file("${path.module}/cloud-config")
}

resource "aws_security_group" "bastion" {
  description = "the bastion security group that allows port 22 from whitelisted ips"

  vpc_id = var.vpc_id
  name   = "bastion_grafana"

  ingress {
    protocol    = "tcp"
    from_port   = 22
    to_port     = 22
    cidr_blocks = var.bastion_whitelist_ips
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

